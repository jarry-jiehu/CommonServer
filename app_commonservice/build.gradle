apply plugin: 'com.android.application'

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "com.stv.commonservice"
        minSdkVersion 14
        targetSdkVersion 27
        versionCode 3243
        versionName "3.2.43"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
        ndk {
            abiFilters 'armeabi-v7a'
        }
    }
    sourceSets {
        main {
            jniLibs.srcDirs = ['libs/jni']
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    //执行lint检查，有任何的错误或者警告提示，都会终止构建，我们可以将其关掉。
    lintOptions {
        abortOnError false
    }
    productFlavors {
    }
    //签名
    signingConfigs {
        debug {
        }
        release {
        }
        /**
         * 918/938/8064/8094/648/NEWC1S/U4/U4C等平台 userdebug 签名。
         * 918/8064/NEWC1S user 签名。
         */
        letv_db {
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
            storeFile file("../.keystore/letv_db.keystore")
        }
        /**
         * 801 user 和 userdebug 签名相同
         */
        letv_db_801 {
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
            storeFile file("../.keystore/letv_db_801.keystore")
        }
        /**
         * 928 userdebug 使用了独立签名
         */
        letv_d_928 {
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
            storeFile file("../.keystore/letv_d_928.keystore")
        }
        /**
         * 928/938/8094/648/U4/U4C等新平台 user 签名。项目中不包含，由服务器进行签名。
         */
        letv_b {
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
            storeFile file("../platform.keystore")
        }
    }
    buildTypes {
        debug {
            buildConfigField "boolean", "LOG_DEBUG", "true"
            minifyEnabled false
            zipAlignEnabled true
            shrinkResources false
        }
        release {
            buildConfigField "boolean", "LOG_DEBUG", "false"
            minifyEnabled false
            zipAlignEnabled true
            shrinkResources false
            proguardFile 'proguard-rules.pro'
        }
        // 918 8064 newc1s user and userdebug. 928 938 8094 648 U4 U4C userdebug.
        letv_db {
            buildConfigField "boolean", "LOG_DEBUG", "false"
            minifyEnabled false
            zipAlignEnabled true
            shrinkResources false
            proguardFile 'proguard-rules.pro'
            signingConfig signingConfigs.letv_db
        }
        // 801 user and userdebug.
        letv_db_801 {
            buildConfigField "boolean", "LOG_DEBUG", "false"
            minifyEnabled false
            zipAlignEnabled true
            shrinkResources false
            proguardFile 'proguard-rules.pro'
            signingConfig signingConfigs.letv_db_801
        }
        // 928 userdebug.
        letv_d_928 {
            buildConfigField "boolean", "LOG_DEBUG", "false"
            minifyEnabled false
            zipAlignEnabled true
            shrinkResources false
            proguardFile 'proguard-rules.pro'
            signingConfig signingConfigs.letv_d_928
        }
        // 928 938 8094 648 U4 U4C user.
        letv_b {
            buildConfigField "boolean", "LOG_DEBUG", "false"
            minifyEnabled false
            zipAlignEnabled true
            shrinkResources false
            proguardFile 'proguard-rules.pro'
            signingConfig signingConfigs.letv_b
        }
    }
    buildTypes.all { type ->
        type.matchingFallbacks = ['release']
    }
    //修改生成apk路径和名字
    applicationVariants.all { variant ->
        def appName = 'CommonService'
        String type = variant.buildType.name
        if (type.equals('release')
                || type.equals('debug')) {
            variant.outputs.all { output ->
                outputFileName = "${appName}" +
                        "-v${defaultConfig.versionName}" +
                        "-${type}" +
                        "-unsigned" +
                        "-${releaseTime()}" +
                        ".apk"
            }
        } else {
            variant.outputs.all {
                outputFileName = "${appName}" +
                        "-v${defaultConfig.versionName}" +
                        "-${type}" +
                        "-${releaseTime()}" +
                        ".apk"
            }
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation fileTree(include: ['*.aar'], dir: 'libs')

    implementation project(path: ':library_base')
    implementation project(path: ':module_business')

    implementation 'com.android.support:multidex:1.0.3'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'com.android.support:support-v4:27.1.1'
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.android.support:design:27.1.1'
    implementation 'com.android.support:recyclerview-v7:27.1.1'
    implementation 'com.android.support:cardview-v7:27.1.1'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

    implementation 'com.github.bumptech.glide:glide:4.9.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.9.0'
    implementation 'com.squareup.picasso:picasso:2.5.2'
    implementation "io.reactivex.rxjava2:rxjava:2.2.8"
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'com.github.tbruyelle:rxpermissions:0.10.2'

    implementation 'com.journeyapps:zxing-android-embedded:3.5.0'
    implementation 'com.google.zxing:core:3.3.3'
    implementation 'com.google.zxing:zxing-parent:3.3.3'
    implementation 'com.squareup.okio:okio:2.2.2'
    implementation 'com.squareup.okhttp3:okhttp:3.12.0'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    //MQTT依赖库
    implementation 'org.eclipse.paho:org.eclipse.paho.client.mqttv3:1.2.1'
    implementation 'org.eclipse.paho:org.eclipse.paho.android.service:1.1.1'
}
